buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:9.0.1'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.application'

def properties = new Properties();
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def gstreamer_sdk_dir = properties.getProperty('gstreamer_sdk.dir')
def boost_build_dir = properties.getProperty('boost_build.dir')

android {
    compileSdkVersion 36
    buildToolsVersion "35.0.0"
    ndkVersion "25.2.9519653"
    namespace "cz.but.telepresence"

    buildFeatures {
        prefab true
    }

    defaultConfig {
        applicationId "cz.but.telepresence"
        // for Vulkan, need at least 24
        minSdkVersion 24
        compileSdkVersion 30

        versionName = "1.0"
        versionCode = 1

        externalNativeBuild {
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
            cmake {
                arguments "-DGSTREAMER_SDK_DIR:STRING=${gstreamer_sdk_dir}"
                arguments "-DBOOST_BUILD_DIR:STRING=${boost_build_dir}"
                arguments "-DANDROID_STL=c++_shared"
                cppFlags ''
            }
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDir 'android_resources'
            java.srcDirs = ['java']
        }
        OpenGLES {
            res.srcDir 'android_resources/opengles'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path file("CMakeLists.txt")
        }
    }

    flavorDimensions 'api'

    productFlavors {
        OpenGLES {
            dimension 'api'
            applicationIdSuffix '.opengles'
            externalNativeBuild.cmake.cppFlags '-DDEFAULT_GRAPHICS_PLUGIN_OPENGLES'

            resValue "string", "app_name", "BUT Telepresence (Opengl ES)"
        }
    }
}

dependencies {
    implementation 'org.khronos.openxr:openxr_loader_for_android:1.1.53'
}

